/*
*  MIT License
*  Copyright (c) 2017 PAIS-Lab-Public-Projects
*
*  Permission is hereby granted, free of charge, to any person obtaining a copy
*  of this software and associated documentation files (the "Software"), to deal
*  in the Software without restriction, including without limitation the rights
*  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
*  copies of the Software, and to permit persons to whom the Software is
*  furnished to do so, subject to the following conditions:
*
*  The above copyright notice and this permission notice shall be included in all
*  copies or substantial portions of the Software.
*
*  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
*  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
*  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
*  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
*  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
*  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
*  SOFTWARE.
*/
group 'CFSM'

ext {
    // CFSM project family version
    projectReleaseVersion = '1.1.0'

    // used libs versions
    scalaVersion = '2.12'
    jodaTimeVersion = '2.9.9'
    scalaVersionFull = "$scalaVersion.5"
    vertxVersion = '3.5.1'
    slf4jVersion = '1.7.25'
    commonsCliVersion = '1.4'
    codacyReporterVersion = '1.0.7'
    junitVersion = '4.12'
    scalaTestVersion = '3.0.4'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'scala'

    dependencies {
        // scala std lib
        compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersionFull
        // test
        testCompile group: 'junit', name: 'junit', version: junitVersion
        testCompile group: 'org.scalatest', name: "scalatest_$scalaVersion", version: scalaTestVersion
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.setDestination(new File("${buildDir}/reports/jacoco/report.xml"))
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

allprojects {

    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
        mavenLocal()
    }


    version projectReleaseVersion
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}