/*
*  MIT License
*  Copyright (c) 2017 PAIS-Lab-Public-Projects
*
*  Permission is hereby granted, free of charge, to any person obtaining a copy
*  of this software and associated documentation files (the "Software"), to deal
*  in the Software without restriction, including without limitation the rights
*  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
*  copies of the Software, and to permit persons to whom the Software is
*  furnished to do so, subject to the following conditions:
*
*  The above copyright notice and this permission notice shall be included in all
*  copies or substantial portions of the Software.
*
*  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
*  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
*  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
*  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
*  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
*  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
*  SOFTWARE.
*/
group 'CFSM'

ext {
    vertxVersion = '3.5.0'
    slf4jVersion = '1.7.25'
    commonsCliVersion = '1.4'
    codacyReporterVersion = '1.0.7'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'jacoco'
    sourceCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
    }

    configurations { codacy }

    dependencies {
        // code coverage
        codacy group: 'com.codacy', name: 'codacy-coverage-reporter', version: codacyReporterVersion
        // test
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    // task for pushing code coverage
    task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
        main = "com.codacy.CodacyCoverageReporter"
        classpath = configurations.codacy
        args = [
                "-l",
                "Java",
                "-r",
                "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        ]

        // the condition for task run
        onlyIf {
            file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml").exists() &&
                    System.env['CODACY_PROJECT_TOKEN'] != null
        }
    }

    // test coverage
    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    // sending code coverage report
    check.dependsOn sendCoverageToCodacy


}

allprojects {
    version '0.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}